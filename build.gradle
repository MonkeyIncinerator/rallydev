repositories {
    mavenCentral();
    maven {url 'http://dl.bintray.com/crc83/maven'}
}

buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'bintray'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'

configurations {
    apihelper
}

dependencies {
    apihelper 'org.sbelei:rally-api-helper:1.1'
	compile fileTree(dir: 'lib', include: '*.jar')
	compile fileTree(dir: 'lib-provided', include: '*.jar')
	compile fileTree(dir: 'lib-idea', include: '*.jar')
}

buildDir = 'bin'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }

    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.sbelei'
            artifactId = 'rallydev'
            version = '1.1'

            from components.java
        }
    }
}

def bintrayApiKey ='NA'
bintray {
    user = 'crc83'
    key = bintrayApiKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'RallyTasksAdapter'
        licenses = ['LGPL']
    }
}

def ideaPath="c:/Program Files (x86)/JetBrains/IntelliJ IDEA 14.0.2"

task copyTasksJars (type: Copy) {
    description 'Copy Task plugin jars from Intellij Idea distribution'
    from "${ideaPath}/plugins/tasks/lib"
    include 'tasks-*.jar'
    into 'lib-provided'
}

task copyIdeaJars (type: Copy) {
    description 'Copy IDEA jars from Intellij Idea distribution'
    from "${ideaPath}/lib"
    include 'util.jar','annotations.jar','openapi.jar','idea.jar'
    into 'lib-idea'
}

task copyRallyApi(type: Copy) {
    from configurations.apihelper
    into "lib"
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installing $project.name"
}